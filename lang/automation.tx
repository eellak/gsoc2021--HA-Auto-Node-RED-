import entity

// === Overall Automation ===
Automation:
    'automation:'
        (
        ('name:' name=ID)
        ('condition:' condition=Condition)
        ('actions:' actions*=Action)
        ('enabled:' enabled=BOOL)
        )#
;

// === Conditions ===
Condition: ConditionGroup | PrimitiveCondition;

ConditionGroup:
    '(' r1=Condition ')' operator=BooleanOperatorType '(' r2=Condition ')'
;

PrimitiveCondition: StringCondition | NumericCondition | BoolCondition;

StringCondition:
    (operand1=[StringAttribute|FQN] operator=StringOperatorType operand2=STRING)
;

NumericCondition:
    (operand1=[NumericAttribute|FQN] operator=NumericOperatorType operand2=INT | FLOAT)
;

BoolCondition:
    (operand1=[BoolAttribute|FQN] operator=BooleanOperatorType operand2=BOOL)
;


StringOperatorType: '~' | '!~' | '==' | '!=';

NumericOperatorType: '>' | '<' | '==' | '!=';

BooleanOperatorType: 'AND' | 'OR' | 'NOT' | 'XOR' | 'NOR' | 'XNOR' | 'NAND';

// === Actions ===
Action:
    FloatAction | IntAction | BoolAction | StringAction
;

IntAction:
    ('-' attribute=[IntAttribute|FQN] ':' value=INT)
;

FloatAction:
    ('-' attribute=[FloatAttribute|FQN] ':' value=STRICTFLOAT)
;

StringAction:
    ('-' attribute=[StringAttribute|FQN] ':' value=STRING)
;

BoolAction:
    ('-' attribute=[BoolAttribute|FQN] ':' value=BOOL)
;

// Fully Qualified Names Rule
FQN: ID('.'ID)*;