import entity

// === Overall Automation ===
Automation:
    'automation:'
        (
        ('name:' name=ID)
        ('condition:' condition=Condition)
        ('actions:' actions*=Action)
        ('enabled:' enabled=BOOL)
        )#
;

// === Conditions ===
Condition: ConditionGroup | PrimitiveCondition;

ConditionGroup:
    '(' r1=Condition ')' operator=BooleanOperatorType '(' r2=Condition ')'
;

PrimitiveCondition: StringCondition | NumericCondition | BoolCondition;

// String Conditions
StringCondition:
    StringR | StringL | StringDouble
;

StringR:
    (operand1=[StringAttribute|FQN|entities.attributes] operator=StringOperatorType operand2=STRING)
;

StringL:
    (operand1=STRING operator=StringOperatorType operand2=[StringAttribute|FQN|entities.attributes])
;

StringDouble:
    (operand1=[StringAttribute|FQN|entities.attributes] operator=StringOperatorType operand2=[StringAttribute|FQN|entities.attributes])
;

// Numeric Conditions
NumericCondition:
    NumericR | NumericL | NumericDouble
;

NumericR:
    (operand1=[NumericAttribute|FQN|entities.attributes] operator=NumericOperatorType operand2=NUMBER)
;

NumericL:
    (operand1=NUMBER operator=NumericOperatorType operand2=[NumericAttribute|FQN|entities.attributes])
;

NumericDouble:
    (operand1=[NumericAttribute|FQN|entities.attributes] operator=NumericOperatorType operand2=[NumericAttribute|FQN|entities.attributes])
;

// Boolean Conditions
BoolCondition:
    BoolR | BoolL | BoolDouble
;

BoolR:
    (operand1=[BoolAttribute|FQN|entities.attributes] operator=BooleanOperatorType operand2=BOOL)
;

BoolL:
    (operand1=BOOL operator=BooleanOperatorType operand2=[BoolAttribute|FQN|entities.attributes])
;

BoolDouble:
    (operand1=[BoolAttribute|FQN|entities.attributes] operator=BooleanOperatorType operand2=[BoolAttribute|FQN|entities.attributes])
;

// Operators
StringOperatorType: '~' | '!~' | '==' | '!=';

NumericOperatorType: '>' | '<' | '==' | '!=';

BooleanOperatorType: 'AND' | 'OR' | 'NOT' | 'XOR' | 'NOR' | 'XNOR' | 'NAND';

// === Actions ===
Action:
    FloatAction | IntAction | BoolAction | StringAction
;

IntAction:
    ('-' attribute=[IntAttribute|FQN|entities.attributes] ':' value=INT)
;

FloatAction:
    ('-' attribute=[FloatAttribute|FQN|entities.attributes] ':' value=STRICTFLOAT)
;

StringAction:
    ('-' attribute=[StringAttribute|FQN|entities.attributes] ':' value=STRING)
;

BoolAction:
    ('-' attribute=[BoolAttribute|FQN|entities.attributes] ':' value=BOOL)
;

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;